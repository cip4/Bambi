import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.4.9'
    id 'edu.sc.seis.macAppBundle' version '2.3.0'
}

sourceCompatibility = 11
targetCompatibility = 11

if (project.hasProperty('projectVersion')) {
    version = project.projectVersion
} else {
    version = 'development'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.cip4.bambi.server.BambiServer'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens:  [
                'name': project.properties['name'],
                'version': project.properties['version'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ss'),
                'buildNumber': project.properties['buildNumber'] == null ? 'n. a.' : project.properties['buildNumber']
        ])
    }
}

application {
    mainClass = 'org.cip4.bambi.server.BambiServer'
}

launch4j {
    mainClassName = 'org.cip4.bambi.server.BambiServer'
    icon = "${projectDir}/src/main/dist/bambi.ico"
    outfile = project.name + '-' + project.version + '.exe'
}

macAppBundle {
    mainClassName = 'org.cip4.bambi.server.BambiServer'
    icon = "${projectDir}/src/main/dist/bambi.icns"
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}

dependencies {
    implementation 'org.cip4.tools.jdfutility:JDFUtility:1.x-SNAPSHOT'
    implementation 'log4j:log4j:1.2.17'
    implementation 'xerces:xercesImpl:2.9.1'
    implementation 'commons-logging:commons-logging-api:1.1'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-io:commons-io:2.4'
    implementation 'javax.mail:mail:1.4.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.3.3'
}
